apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

def getPropertyOrNull(String prop) {
    return hasProperty(prop) ? getProperty(prop) : null
}

install {
    repositories.mavenInstaller {
        pom {
            project {
                name project.name
                description 'Android componentization initializer'
                url 'https://github.com/cheie/initializer'

                scm {
                    url 'https://github.com/cheie/initializer'
                    connection 'https://github.com/cheie/initializer.git'
                    developerConnection 'https://github.com/cheie/initializer.git'
                }

                licenses {
                    license {
                        name 'MIT'
                        url 'https://opensource.org/licenses/mit-license'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'chrnie'
                        name 'ChenRenJie'
                    }
                }
            }
        }
    }
}

bintray {
    user = getPropertyOrNull('bintrayUser')
    key = getPropertyOrNull('bintrayKey')
    configurations = ['archives']

    pkg {
        repo = 'initializer'
        name = project.name
        licenses = ['MIT']
        vcsUrl = 'https://github.com/cheie/initializer.git'
    }
}

if (project.plugins.hasPlugin('com.android.library')) {
    task androidSourcesJar(type: Jar) {
        classifier = 'sources'
        from android.sourceSets.main.java.source
    }

    artifacts {
        archives androidSourcesJar
    }
} else {
    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    artifacts {
        archives sourcesJar
    }
}
